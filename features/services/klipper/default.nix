{ config, lib, pkgs, ... }:
let
  cfg = config.my.features.services.klipper;
in
{
  options.my.features.services.klipper = {
    enable = lib.mkEnableOption "Klipper 3D Printing Stack";
    expose = {
      enable = lib.mkEnableOption "Expose Fluidd via Caddy";
      subdomain = lib.mkOption {
        type = lib.types.str;
        default = "fluidd";
        description = "Subdomain to use";
      };
      auth = lib.mkEnableOption "Protect with Authentik (Forward Auth)";
    };
  };

  config = lib.mkIf cfg.enable {
    
    # 1. Klipper Service
    services.klipper = {
      enable = true;
      user = "klipper";
      group = "klipper";
      mutableConfig = true; 
      # We provide a basic valid config as a fallback/initial seed
      configFile = pkgs.writeText "klipper-init.cfg" ''
        [mcu]
        serial: /dev/null
        [printer]
        kinematics: none
        max_velocity: 1000
        max_accel: 1000
      '';
    };

    # Explicitly define user and group to avoid assertion errors
    users.groups.klipper = {};
    users.users.klipper = {
      isSystemUser = true;
      group = "klipper";
      extraGroups = [ "dialout" ];
    };

    # 2. Moonraker Service
    services.moonraker = {
      enable = true;
      user = "klipper";
      group = "klipper";
      address = "0.0.0.0";
      port = 7125;
      allowSystemControl = true;
      
      settings = {
        authorization = {
          force_logins = false;
          cors_domains = [
            "*.fls.ancoris.ovh"
            "https://fluidd.fls.ancoris.ovh"
            "http://localhost"
            "http://127.0.0.1"
            "https://app.fluidd.xyz"
            "http://app.fluidd.xyz"
          ];
          trusted_clients = [
            "10.0.0.0/8"
            "127.0.0.0/8"
            "192.168.0.0/16"
            "100.0.0.0/8"
          ];
        };
        file_manager = {
            enable_object_processing = true;
        };
        octoprint_compat = {};
        history = {};

        # Include the secure config generated by sops template
        "include ${config.sops.templates."moonraker_power.conf".path}" = {};

        "webcam bed" = {
          location = "printer";
          stream_url = "https://cam.moonraker.fls.ancoris.ovh/?action=stream";
          snapshot_url = "https://cam.moonraker.fls.ancoris.ovh/?action=snapshot";
          flip_vertical = true;
        };
      };
    };

    # Secrets Management
    sops.secrets.moonraker_hass_token = {
      sopsFile = ../../../secrets/secrets.yaml;
      owner = "klipper";
      group = "klipper";
    };

    sops.templates."moonraker_power.conf" = {
      owner = "klipper";
      group = "klipper";
      content = ''
        [power homeassistant_switch]
        type: homeassistant
        protocol: https
        address: hass.fls.ancoris.ovh
        port: 443
        device: switch.sonoff_basic_ender_sonoff_basic_relay
        token: ${config.sops.placeholder.moonraker_hass_token}
        domain: switch
        on_when_job_queued: True
        locked_while_printing: True
        restart_klipper_when_powered: True
      '';
    };

    # 3. Webcam Service (mjpg-streamer)
    services.mjpg-streamer = {
      enable = true;
      inputPlugin = "input_uvc.so -d /dev/video0 -r 1280x720 -f 30";
      outputPlugin = "output_http.so -p 8081 -w ${pkgs.mjpg-streamer}/share/mjpg-streamer/www";
    };

    # 4. Fluidd, Moonraker and Webcam via Caddy
    services.caddy.virtualHosts = {
      "${cfg.expose.subdomain}.${config.my.features.services.caddy.baseDomain}" = lib.mkIf cfg.expose.enable {
        extraConfig = ''
          root * ${pkgs.fluidd}/share/fluidd/htdocs
          file_server
          
          reverse_proxy /websocket /printer/* /api/* /access/* /machine/* /server/* 127.0.0.1:7125

          ${lib.optionalString cfg.expose.auth "import authentik"}
        '';
      };

      "cam.moonraker.${config.my.features.services.caddy.baseDomain}" = lib.mkIf cfg.expose.enable {
        extraConfig = ''
          reverse_proxy 127.0.0.1:8081
        '';
      };
    };
  };
}